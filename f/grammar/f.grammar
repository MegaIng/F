file: _statement+
_statement: (_value|assignment) ";"
assignment: NAME ":=" _value
_value: infix_operation_1
?infix_operation_1: infix_operation_2 (INFIX_OPERATOR_1 infix_operation_2)*
?infix_operation_2: infix_operation_3 (INFIX_OPERATOR_2 infix_operation_3)*
?infix_operation_3: infix_operation_4 (INFIX_OPERATOR_3 infix_operation_4)*
?infix_operation_4: infix_operation_5 (INFIX_OPERATOR_4 infix_operation_5)*
?infix_operation_5: _call_value (NAME ":" _call_value)*
_call_value: call
     | escaped_value
escaped_value: code_block
             | list
             | "(" (PREFIX_OPERATOR|INFIX_OPERATOR) ")"
             | "(" _value ")"
             | PREFIX_OPERATOR escaped_value -> prefix_operator
             | STRING
             | NUMBER
             | NAME
             | escaped_value "()" -> empty_call

?call: simple_call|extended_call|variadic_call
simple_call: escaped_value escaped_value+
!variadic_call: escaped_value escaped_value* ELLIPSIS escaped_value*

// ec = extended_call
extended_call: escaped_value escaped_value* ec_parameter escaped_value* ec_code_block
ec_parameter: "|" (NAME "(" _value ")")* NAME* "|"
ec_code_block: "[" _statement* _value "]"

list: "{" escaped_value* "}"
code_block: "[" [parameter] _statement* _value "]"
parameter: "|" NAME* [ELLIPSIS] "|"

PREFIX_OPERATOR: "!"|"-"|"+"

INFIX_OPERATOR: INFIX_OPERATOR_1|INFIX_OPERATOR_2|INFIX_OPERATOR_3|INFIX_OPERATOR_4
INFIX_OPERATOR_1: "<-"
INFIX_OPERATOR_2: "<"|"<="|"="|">="|">"
INFIX_OPERATOR_3: "*"|"/"|"%"
INFIX_OPERATOR_4: "+"|"-"

STRING: /"(?:[^"\\\n]|\\.)*"/
NAME: /\b[a-zA-Z_][a-zA-Z_0-9]*\b/
COMMENT: "//" /.*/ NEWLINE
ELLIPSIS: "..."

NUMBER: /(?<![0-9])([0-9]+(\.[0-9]*)?|[0-9]*\.[0-9]+)(e[0-9]+)?(?![0-9])/
%import common.WS
%import common.NEWLINE
%ignore COMMENT
%ignore WS
