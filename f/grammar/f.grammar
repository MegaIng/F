file: _value [";"] _EOI

_value: semicolon_operation

!?semicolon_operation: (assignment|infix_operation_1) (";" (assignment|infix_operation_1))*

assignment: NAME ":=" infix_operation_1

!?infix_operation_1: infix_operation_2 ("<-" infix_operation_2)*
!?infix_operation_2: infix_operation_3 (("<"|"<="|"="|">="|">") infix_operation_3)*
!?infix_operation_3: infix_operation_4 (("**") infix_operation_4)*
!?infix_operation_4: infix_operation_5 (("*"|"/"|"%") infix_operation_5)*
!?infix_operation_5: infix_operation_6 (("+"|"-") infix_operation_6)*
?infix_operation_6: _call_value (NAME ":" _call_value)*
_call_value: call
     | escaped_value
!escaped_value: code_block
             | list
             | _ESCAPED_OPERATOR
             | _OPEN _value ")" // "("
             | ("!"|"-"|"+") escaped_value -> prefix_operator
             | STRING
             | NUMBER
             | NAME
             | (ELLIPSIS|"...(" _value ")") -> variadic_value // ELLIPSIS ["(" _value ")"]
             | escaped_value _OPEN_CLOSE -> empty_call

_ESCAPED_OPERATOR: "(" ("<-"|"<"|"<="|"="|">="|">"|"**"|"*"|"/"|"%"|"+"|"-"|"!") ")"
_OPEN_CLOSE: "()"
_OPEN: /\((?!\))/

?call: simple_call|extended_call
simple_call: escaped_value escaped_value+

// ec = extended_call
extended_call: escaped_value escaped_value* ec_parameters escaped_value* ec_code_block
ec_parameters: "|" (NAME _OPEN _value ")")* NAME* "|"
ec_code_block: "[" _value [";"] "]"

list: "{" escaped_value* "}"
code_block: "[" [parameters] _value [";"] "]"
parameters: "|" NAME* [(NAMED_ELLIPSIS|ELLIPSIS) NAME*] "|"


STRING: /"(?:[^"\\\n]|\\.)*"/
NAME: /\b[a-zA-Z_][a-zA-Z_0-9]*\b/
COMMENT: "//" /.*\n/
ELLIPSIS: /\.\.\.(?!\()/ // "..."
NAMED_ELLIPSIS: ELLIPSIS NAME

NUMBER: /(?<![0-9])([0-9]+(\.[0-9]*)?|[0-9]*\.[0-9]+)(e[0-9]+)?(?![0-9])/
_EOI: "<EOI>"
%import common.WS
%ignore COMMENT
%ignore WS
