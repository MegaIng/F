file: _statements

_statements: _value (";" _value)* [";"]
_value: assignment|infix_operation_1
assignment: NAME ":=" _value

?infix_operation_1: infix_operation_2 (INFIX_OPERATOR_1 infix_operation_2)*
?infix_operation_2: infix_operation_3 (INFIX_OPERATOR_2 infix_operation_3)*
?infix_operation_3: infix_operation_4 (INFIX_OPERATOR_3 infix_operation_4)*
?infix_operation_4: infix_operation_5 (INFIX_OPERATOR_4 infix_operation_5)*
?infix_operation_5: _call_value (NAME ":" _call_value)*
_call_value: call
     | escaped_value
escaped_value: code_block
             | list
             | "(" (PREFIX_OPERATOR|INFIX_OPERATOR) ")"
             | "(" _value ")"
             | PREFIX_OPERATOR escaped_value -> prefix_operator
             | STRING
             | NUMBER
             | NAME
             | ELLIPSIS ["(" _value ")"] -> variadic_value
             | escaped_value "()" -> empty_call

?call: simple_call|extended_call
simple_call: escaped_value escaped_value+

// ec = extended_call
extended_call: escaped_value escaped_value* ec_parameters escaped_value* ec_code_block
ec_parameters: "|" (NAME "(" _value ")")* NAME* "|"
ec_code_block: "[" _statements "]"

list: "{" escaped_value* "}"
code_block: "[" [parameters] _statements "]"
parameters: "|" NAME* [NAMED_ELLIPSIS|ELLIPSIS] "|"

PREFIX_OPERATOR: "!"|"-"|"+"

INFIX_OPERATOR: INFIX_OPERATOR_1|INFIX_OPERATOR_2|INFIX_OPERATOR_3|INFIX_OPERATOR_4
INFIX_OPERATOR_1: "<-"
INFIX_OPERATOR_2: "<"|"<="|"="|">="|">"
INFIX_OPERATOR_3: "*"|"/"|"%"
INFIX_OPERATOR_4: "+"|"-"

STRING: /"(?:[^"\\\n]|\\.)*"/
NAME: /\b[a-zA-Z_][a-zA-Z_0-9]*\b/
COMMENT: "//" /.*/ NEWLINE
ELLIPSIS: "..."
NAMED_ELLIPSIS: ELLIPSIS NAME

NUMBER: /(?<![0-9])([0-9]+(\.[0-9]*)?|[0-9]*\.[0-9]+)(e[0-9]+)?(?![0-9])/
%import common.WS
%import common.NEWLINE
%ignore COMMENT
%ignore WS
